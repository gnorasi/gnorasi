####################################################
# Configuration file for Voreen.
# Rename this file to 'config.txt' and edit.
####################################################

######################
# Module configuration
######################
# See modules/ for a list of all available modules.
# Unix library paths may be adjusted in "Paths" section at the bottom of this file.

# We definitely need this.
VRN_MODULES += base

# We add default Voreen modules here, i.e., those which do not depend on
# non-standard external libraries (see modules/default.pri).
VRN_MODULES += default

# ZIP support via the zlib library.
VRN_MODULES += zip

# Image I/O and processing using the DevIL library.
VRN_MODULES += devil

# GPU-based isosurface extraction and rendering, requires the HPMC library.
#VRN_MODULES += hpmc

# DCMTK DICOM module, requires the DCMTK library.
#VRN_MODULES += dcmtk 
# Select DCMTK version:
#DEFINES += VRN_DCMTK_VERSION_354
#DEFINES += VRN_DCMTK_VERSION_360

# Video encoding using the FFmpeg library.
#VRN_MODULES += ffmpeg

# Font rendering via the freetype/ftgl libraries.
#VRN_MODULES += fontrendering

# Python bindings, allowing scripts to access Voreen API.
#VRN_MODULES += python

# Volume reader for multi-image TIFF files, requires the libtiff library.
#VRN_MODULES += tiff

# OpenCL wrapper and OpenCL-based processors.
#VRN_MODULES += opencl

# OpenMP
#VRN_MODULES += openmp


###################
# Projects to build
###################
# Only for unix, not considered by qmake-default.bat on Windows.
VRN_PROJECTS = tgt core qt voreenve

# Also build the other applications?
#VRN_PROJECTS += voltool
#VRN_PROJECTS += simple-glut
#VRN_PROJECTS += simple-qt

#####################
# Additional settings
#####################

# From Voreen Snapshot
DEFINES += VRN_SNAPSHOT

# Link Voreen libraries dynamically
DEFINES += VRN_DYNAMIC_LIBS

# Activate additional debug code in Voreen (also sets TGT_DEBUG and activates assertions)
DEFINES += VRN_DEBUG

# Disable this line in case of problems with PCH
DEFINES += VRN_PRECOMPILE_HEADER

# Disables generation of module registration header 'modules/gen_moduleregistration.h'
# and uses static 'modules/moduleregistration.h' instead.
# The registration header is included by the VoreenApplication for autoloading of modules.
#DEFINES += VRN_NO_REGISTRATION_HEADER_GENERATION

# Enable compiler warnings
CONFIG += warn_on

# Write log output into a HTML file
DEFINES += VRN_ADD_FILE_LOGGER

# Compile Voreen for deployment (adjusts resource paths)
#DEFINES += VRN_DEPLOYMENT

# Extract SVN revision from working copy into svnversion.h
#DEFINES += VRN_WITH_SVNVERSION

# Show splash screen
DEFINES += VRN_SPLASHSCREEN

# Do not use stylesheet in VoreenVE
#DEFINES += VRN_NO_STYLESHEET

# Add additional include paths
INCLUDEPATH +=

# Add additional library search paths
LIBDIR +=

# Disable optimization (useful for debugging)
#QMAKE_CXXFLAGS_RELEASE -= -O2
#QMAKE_CXXFLAGS_RELEASE += -O0

# Make all warnings into errors
#QMAKE_CXXFLAGS += -Werror


############################
# Platform-specific settings
############################
unix {
  CONFIG -= release
  CONFIG += debug

  # For installation with 'make install' specify the installation prefix
  # here. When this is set, the applications cannot be started from the
  # build directory, they need to be installed first.
  #INSTALL_PREFIX = /usr/local

  # Disable usage of libwrap, sometimes needed with DCMTK
  #CONFIG += without_libwrap
}

unix: !macx {
  # Building shared library is the default on Linux
  CONFIG -= staticlib
  CONFIG += dll
}

win32: !win32-g++ {
    # Use Windows Management Instrumentation (WMI) for hardware detection
    # on Windows? (Not supported for MinGW)
    DEFINES += TGT_WITH_WMI
    
    # allows 32 Bit builds to use more than 2GB RAM (VC++ only)
    QMAKE_LFLAGS += /LARGEADDRESSAWARE
    
    # enable/disable incremental linking for debug builds
    CONFIG(debug, debug|release) {
        QMAKE_LFLAGS += /INCREMENTAL
        #QMAKE_LFLAGS += /INCREMENTAL:NO
    }
}

###############
# Default paths
###############

# Path to Voreen home, detected automatically
VRN_HOME = $$PWD

unix {
  FREETYPE_DIR = /usr/include/freetype2
  PYTHON_DIR = /usr/include/python
  PYTHON_LIBS = -lpython2.6
}

macx {
  PYTHON_DIR = /usr/include/python2.5
  GLEW_DIR = /opt/local/include
  GLEW_LIBS = -l/opt/local/lib/libGLEW

}

### Local Variables:
### mode:conf-unix
### End:
